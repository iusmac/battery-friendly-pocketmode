import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'com.android.application'
    id 'kotlin-kapt'
    id 'org.jetbrains.kotlin.android'
}

android {
    namespace 'com.github.iusmac.pocketjudge'

    defaultConfig {
        applicationId 'com.github.iusmac.pocketjudge'
        minSdk 31
        targetSdk 34
        versionCode 3
        versionName '2.0'
    }

    signingConfigs {
        platform {
            storeFile file('platform.jks')
            storePassword 'android'
            keyAlias 'platform'
            keyPassword 'android'
        }
    }

    buildTypes {
        all {
            signingConfig signingConfigs.platform
        }
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'src/main/proguard.flags'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    tasks.withType(KotlinCompile).configureEach {
        compilerOptions.jvmTarget = JvmTarget.JVM_17
    }

    sourceSets {
        main {
            java.srcDirs = ['src/main/java/']
        }
    }

    lint {
        disable 'ProtectedPermissions'
        informational 'MissingTranslation'
    }
}

dependencies {
    implementation libs.core
    implementation libs.preference

    implementation libs.hilt_android
    kapt libs.hilt_compiler

    implementation project(path: ':SettingsLib:CollapsingToolbarBaseActivity')
    implementation project(path: ':SettingsLib:FooterPreference')
    implementation project(path: ':SettingsLib:LayoutPreference')
}

kapt {
    correctErrorTypes true
}
